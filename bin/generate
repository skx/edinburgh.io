#!/usr/bin/perl
#
#  To script to read ./data/*.xml and generate a series of static
# html-files.
#
#  The code is a little ropy, but will be improved.
#
# Steve
# --


use strict;
use warnings;

use File::Find;
use File::Path qw/ mkpath /;
use HTML::Template;



#
#  Configuration variables.
#
my %CONFIG;
$CONFIG{ 'input' }   = "./data";
$CONFIG{ 'output' }  = "./htdocs";
$CONFIG{ 'verbose' } = 1;


#
#  The data for all our pubs.
#
my %DATA;

#
#  Find all the XML files for our pubs.
#
#  Build into an (inefficient) data-structure.
#
$CONFIG{ 'verbose' } && print "Finding files beneath: $CONFIG{'input'}\n";
find( { wanted => \&process, no_chdir => 1 }, $CONFIG{ 'input' } );

#
#  Create a single page for each venue.
#
#  TODO: We need some way of marking venues as having been renamed
#
#   e.g.  The current "leith beer co" is the old "waterline".
#
createVenues();


#
#  Create the A-Z & letter-pages
#
createA2Z();


#
#  Create index page + .txt
#
createIndex();

#
#  All done
#
exit(0);



#
# Process *.xml.
#
sub process
{
    my ($file) = $File::Find::name;
    return unless ( $file =~ /\.xml$/ );

    $CONFIG{ 'verbose' } && print "Parsing: $file\n";

    open( my $data, "<", $file ) or
      die "Failed to read $file - $!";
    foreach my $line (<$data>)
    {
        chomp($line);
        if ( $line =~ /^\s+<([^>]+)>(.*)<\/[^>]+>$/ )
        {
            $DATA{ $file }{ $1 } = $2;
        }
    }
    close($data);
}


#
#  Create one page for each venue.
#
sub createVenues
{

    #
    #  Now process each venue.
    #
    foreach my $venue ( keys %DATA )
    {

        #
        #  Load the template
        #
        my @venue = loadTemplate("venue.tmpl");

        my $template =
          HTML::Template->new( arrayref          => \@venue,
                               die_on_bad_params => 0 );

        #
        #  The images for this pub
        #
        my $images = undef;
        my $sname  = $DATA{ $venue }{ 'sname' };
        foreach my $img ( sort( glob("images/$sname*") ) )
        {
            push( @$images, { path => $img } );
        }

        $sname .= ".html";

        my $name = $DATA{ $venue }{ 'name' };
        $name =~ s/^the //gi;
        my $beg = substr( $name, 0, 1 );
        $beg = lc($beg);

        $template->param( $DATA{ $venue } );
        $template->param( letter => $beg );

        if ($images)
        {
            $template->param( images => $images, has_images => 1 );
        }
        else
        {
            print "WARNING: $sname missing images\n";
        }

        #
        #  Output.
        #
        if ( !-d $CONFIG{ 'output' } . "/pubs" )
        {
            mkpath( $CONFIG{ 'output' } . "/pubs", { verbose => 0 } );
        }

        open( OUTPUT, ">", $CONFIG{ 'output' } . "/pubs/" . $sname );
        print OUTPUT $template->output();
        close(OUTPUT);
    }

}


#
#  Create a-z
#
sub createA2Z
{

    #
    #  Process each letter.
    #
    my $letters;

    foreach my $letter ( 'a' ... 'z' )
    {
        my $loop = undef;

        #
        # Look for venues with that name.
        #
        foreach my $tmp ( keys %DATA )
        {
            my $name = $DATA{ $tmp }{ 'name' };
            $name =~ s/^the //gi;
            my $beg = substr( $name, 0, 1 );
            $beg = lc($beg);

            if ( $beg eq $letter )
            {
                push( @$loop, $DATA{ $tmp } );
            }
        }


        my $count = undef;
        $count = 1 if ($loop);
        push( @$letters,
              {  letter => $letter,
                 count  => $count
              } );

        my @letter = loadTemplate("letter.tmpl");

        my $template =
          HTML::Template->new( arrayref          => \@letter,
                               die_on_bad_params => 0 );

        $template->param( loop => $loop ) if ($loop);
        $template->param( letter => $letter );

        #
        #  Open.
        #
        if ( !-d $CONFIG{ 'output' } . "/pubs/a-z/$letter/" )
        {
            mkpath( $CONFIG{ 'output' } . "/pubs/a-z/$letter/",
                    { verbose => 0 } );
        }

        open( my $handle, ">", "$CONFIG{'output'}/pubs/a-z/$letter/index.html" )
          or
          die "Failed to open: $!";
        print $handle $template->output();
        close($handle);

    }

    #
    #  Now the a-z
    #
    my @az = loadTemplate("a-z.tmpl");
    my $template = HTML::Template->new( arrayref          => \@az,
                                        die_on_bad_params => 0 );

    $template->param( letters => $letters );
    open( my $handle, ">", "$CONFIG{'output'}/pubs/a-z/index.html" );
    print $handle $template->output();
    close($handle);

}


sub createIndex
{

    #
    #  Finally output the XML file.
    #
    my @txt = loadTemplate("index.txt.tmpl");
    my $template = HTML::Template->new( arrayref          => \@txt,
                                        die_on_bad_params => 0 );

    my $xml;
    foreach my $key (
        sort {
            lc $DATA{ $a }{ 'name' } cmp lc $DATA{ $b }{ 'name' }
        } keys %DATA
      )
    {
        push( @$xml, $DATA{ $key } );
    }
    $template->param( xml => $xml );
    open( my $xhandle, ">", "$CONFIG{'output'}/pubs/index.txt" );
    print $xhandle $template->output();
    close($xhandle);



    #
    #  Here.
    #
    my @ind = loadTemplate("index.html.tmpl");
    $template = HTML::Template->new( arrayref          => \@ind,
                                     die_on_bad_params => 0 );
    $template->param( coords => $xml );
    open( my $ihandle, ">", "$CONFIG{'output'}/pubs/index.html" );
    print $ihandle $template->output();
    close($ihandle);

}


=begin doc

Load a "file" from beneath the "__DATA__" section.

This resets the DATA handle to allow re-reading more than once, if
necessary, and only works because the __DATA__ section is formatted
as follows:

=for example begin

filename:
  DATA - prefixed by two spaces.
  DATA
filename2:
  DATA - prefixed by two spaces.
  ..

=for example end

=cut

sub loadTemplate
{
    my ($file) = (@_);

    # save the position
    my $data_pos = tell DATA;

    my %hash;
    my $filename = undef;

    while ( my $line = <DATA> )
    {
        chomp($line);
        if ( $line =~ /^  (.*)/ )
        {
            $hash{ $filename } .= $1 . "\n";
        }
        elsif ( $line =~ /^(.*):$/ )
        {
            $filename = $1;
        }
    }

    # reset
    seek DATA, $data_pos, 0;

    my $content = $hash{ $file } || undef;
    die "File not found in __DATA__ seciont: $file"
      if ( !$content );
    return ($content);
}

__DATA__
venue.tmpl:
  <!DOCTYPE html>
  <html>
  <head>
  <title>Edinburgh Pubs: <!-- tmpl_var name='name' --></title>
  <link href="/css/style.css" rel="stylesheet" media="all">
  <script src="/js/jquery.min.js"></script>
  <script src="/js/portfolio.pack.min.js"></script>
  <script src="http://maps.googleapis.com/maps/api/js?key=AIzaSyDY0kkJiTPVd2U7aTOAwhc9ySH6oHxOIYM&sensor=false"></script>
  <script>
  var myCenter=new google.maps.LatLng(<!-- tmpl_var name='latitude' -->,<!-- tmpl_var name='longditude' -->);
  function initialize()
  {
    var mapProp = { center:myCenter, zoom:14, mapTypeId:google.maps.MapTypeId.ROADMAP  };
  var map=new google.maps.Map(document.getElementById("googleMap"),mapProp);
  var marker=new google.maps.Marker({ position:myCenter  });
  marker.setMap(map);
  var infowindow = new google.maps.InfoWindow({
     content:"<!-- tmpl_var name='name' -->"  });
  infowindow.open(map,marker);
  }
  google.maps.event.addDomListener(window, 'load', initialize);
  </script>
  <script type="text/javascript">
    $(document).ready(function() {
            var p = $("#gallery").portfolio();
            p.init();
    });
  </script>
  </head>
  <body>
  <h1><a href="http://edinburgh.io/pubs/">Edinburgh Pubs</a> - <!-- tmpl_var name='name' --></h1>
  <h2>Summary</h2>
  <blockquote>
  <!-- tmpl_var name='address' -->
  <!-- tmpl_if name='telephone' -->
   <p>Telephone: <!-- tmpl_var name='telephone' --></p>
  <!-- /tmpl_if -->
  <!-- tmpl_if name='website' -->
   <p>Website: <a href="<!-- tmpl_var name='website' -->"><!-- tmpl_var name='website' --></a></p>
  <!-- /tmpl_if -->
  </blockquote>
  <!-- tmpl_if name='has_images' -->
  <h2>Images</h2>
  <blockquote>
  <div id="gallery">
  <!-- tmpl_loop name='images' -->
    <img data-src="/<!-- tmpl_var name='path' -->" />
  <!-- /tmpl_loop -->
  </div>
  </blockquote>
  <!-- /tmpl_if name='images' -->
  <h2>Map</h2>
  <div style="text-align:center;width:100%;">
  <div id="googleMap" style="display:inline-block;width:500px;height:380px;"></div>
  </div>
  <h2>History</h2>
  <blockquote>
  <p>TODO.</p>
  </blockquote>
  </body>
  </html>
letter.tmpl:
  <html>
   <head>
    <title>Pubs beginning with <!-- tmpl_var name='letter' --></title>
    <link href="/css/style.css" rel="stylesheet" media="all">
  </head>
  <body>
   <h1>Pubs beginning with <!-- tmpl_var name='letter' --></h1>
   <ul>
   <!-- tmpl_loop name='loop' -->
   <li><a href="/pubs/<!-- tmpl_var name='sname' -->.html"><!-- tmpl_var name='name' --></a></li>
   <!-- /tmpl_loop -->
   </ul>
  </body>
  </html>
a-z.tmpl:
  <html>
   <head>
    <title>Edinburgh A-Z of Pubs</title>
    <link href="/css/style.css" rel="stylesheet" media="all">
  </head>
  <body>
   <h1>Edinburgh A-Z of Pubs</h1>
  <ul style="list-style:none;">
  <!-- tmpl_loop name='letters' -->
    <!-- tmpl_if name='count' -->
      <li><a href="/a-z/<!-- tmpl_var name='letter' -->/"><!-- tmpl_var name='letter' --></a></li>
    <!-- tmpl_else -->
      <li><!-- tmpl_var name='letter' --></li>
    <!-- /tmpl_if -->
  <!-- /tmpl_loop -->
  </ul>
index.txt.tmpl:
  <!-- tmpl_loop name='xml' -->
  <pub>
  <name><!-- tmpl_var name='name' --></name>
  <address><!-- tmpl_var name='address' --></address>
  <latitude><!-- tmpl_var name='latitude' --></latitude>
  <longditude><!-- tmpl_var name='longditude' --></longditude>
  <website><!-- tmpl_var name='website' --></website>
  </pub>
  <!-- /tmpl_loop -->
index.html.tmpl:
  <!DOCTYPE html>
  <html>
  <head>
  <title>Edinburgh Pubs</title>
  <link href="/css/style.css" rel="stylesheet" media="all">
  <script src="/js/jquery.min.js"></script>
  <script src="/js/portfolio.pack.min.js"></script>
  <script src="http://maps.googleapis.com/maps/api/js?key=AIzaSyDY0kkJiTPVd2U7aTOAwhc9ySH6oHxOIYM&sensor=false"></script>
  <script>
  var infowindow;
  (function () {
    google.maps.Map.prototype.markers = new Array();
     google.maps.Map.prototype.addMarker = function(marker) {
     this.markers[this.markers.length] = marker;
    };
    google.maps.Map.prototype.getMarkers = function() {
     return this.markers
    };
    google.maps.Map.prototype.clearMarkers = function() {
    if(infowindow) {
      infowindow.close();
    }
    for(var i=0; i<this.markers.length; i++){
      this.markers[i].set_map(null);
    }
  };
  })();
  function createMarker(map,latlng,name,link) {
    var marker = new google.maps.Marker({position: latlng, map: map});
    google.maps.event.addListener(marker, "click", function() {
      if (infowindow) infowindow.close();
      infowindow = new google.maps.InfoWindow({content: "<div><p><a href=\"" + link + "\">" + name + "</a></p></div>" });
      infowindow.open(map, marker);
    });
    return marker;
  }
  var myCenter=new google.maps.LatLng(55.97561,-3.17024);
  function initialize()
  {
  var mapProp = { center:myCenter, zoom:14, mapTypeId:google.maps.MapTypeId.ROADMAP  };
  var map=new google.maps.Map(document.getElementById("googleMap"),mapProp);
  var coords = [
  <!-- tmpl_loop name='coords' -->
  [<!-- tmpl_var name='latitude' -->,<!-- tmpl_var name='longditude' -->,"<!-- tmpl_var name='name' -->", "/pubs/<!-- tmpl_var name='sname' -->.html"],
  <!-- /tmpl_loop -->
  ];

  // iterate over your coords array
  for (var i = 0; i < coords.length; i++) {
        // create a closure for your latitude/longitude pair
        (function(coord) {
            // set the location...
            var latLng = new google.maps.LatLng(coord[0], coord[1]);
            map.addMarker(createMarker(map,latLng,coord[2],coord[3]));
        })(coords[i]);
    };
  }
  google.maps.event.addDomListener(window, 'load', initialize);
  </script>
  </head>
  <body>
  <h1><a href="http://edinburgh.io/pubs/">Edinburgh Pubs</a></h1>
  <div style="text-align:center;width:100%;">
  <div id="googleMap" style="display:inline-block;width:80%;height:450px;"></div>
  </div>
  <p style="text-align:right;"><small>Data generated from <a href="index.txt">simple meta-data</a>, which is open to collaboration <a href="https://github.com/skx/edinburgh.io/">on github</a>.</small></p>
  </body>
  </html>
