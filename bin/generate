#!/usr/bin/perl
#
#  To script to read ./data/*.xml and generate a series of static
# html-files.
#
#  The code is a little ropy, but will be improved.
#
# Steve
# --


use strict;
use warnings;

use File::Find;
use File::Path qw/ mkpath /;
use HTML::Template;
use JSON;


#
#  Configuration variables.
#
my %CONFIG;
$CONFIG{ 'input' }   = "./data";
$CONFIG{ 'output' }  = "./htdocs";
$CONFIG{ 'verbose' } = 1;


#
#  The data for all our pubs.
#
my %DATA;

#
#  Find all the XML files for our pubs.
#
#  Build into an (inefficient) data-structure.
#
$CONFIG{ 'verbose' } && print "Finding files beneath: $CONFIG{'input'}\n";
find( { wanted => \&process, no_chdir => 1 }, $CONFIG{ 'input' } );

#
#  Create a single page for each venue.
#
#  TODO: We need some way of marking venues as having been renamed
#
#   e.g.  The current "leith beer co" is the old "waterline".
#
createVenues();


#
#  Create the A-Z & letter-pages
#
createA2Z();


#
#  Create the data page
#
createDataPage();


#
#  Create recent page.
#
createRecentPage();


#
#  All done
#
exit(0);



#
# Process *.xml.
#
sub process
{
    my ($file) = $File::Find::name;
    return unless ( $file =~ /\.xml$/ );

    $CONFIG{ 'verbose' } && print "Parsing: $file\n";

    open( my $data, "<", $file ) or
      die "Failed to read $file - $!";
    foreach my $line (<$data>)
    {
        chomp($line);
        if ( $line =~ /^\s+<([^>]+)>(.*)<\/[^>]+>$/ )
        {
            $DATA{ $file }{ $1 } = $2;
        }
    }
    close($data);
}


#
#  Create one page for each venue.
#
sub createVenues
{

    #
    #  Load the template page we'll use for the output.
    #
    my @venue = loadTemplate("venue.tmpl");

    #
    #  For each venue.
    #
    foreach my $venue ( keys %DATA )
    {

        my $template =
          HTML::Template->new( arrayref          => \@venue,
                               die_on_bad_params => 0 );

        #
        #  The images for this pub
        #
        my $images = undef;
        my $sname  = $DATA{ $venue }{ 'sname' };
        foreach my $img ( sort( glob("images/$sname*") ) )
        {
            push( @$images, { path => $img } );
        }

        #
        #  Setup past names.
        #
        my $prev = $DATA{ $venue }{ 'previously' };
        delete $DATA{ $venue }{ 'previously' };

        if ($prev)
        {
            my $previous = undef;
            foreach my $past ( split( /,/, $prev ) )
            {

                #
                #  Split
                #
                if ( $past =~ /(.*)\[(.*)\]/ )
                {
                    my $name  = $1;
                    my $sname = $2;
                    push( @$previous,
                          {  name  => $name,
                             sname => $sname
                          } );
                }
            }
            $DATA{ $venue }{ 'previously' } = $previous if ($previous);
        }


        #
        #  The short-name should have a .html suffix, if it doesn't.
        #
        $sname .= ".html" unless ( $sname =~ /\.html$/i );

        #
        #  Set the data into the template.
        #
        $template->param( $DATA{ $venue } );

        if ($images)
        {
            $template->param( images => $images, has_images => 1 );
        }
        else
        {
            print "WARNING: $sname missing images\n";
        }

        #
        #  Ensure the output directory exists..
        #
        if ( !-d $CONFIG{ 'output' } . "/pubs" )
        {
            mkpath( $CONFIG{ 'output' } . "/pubs", { verbose => 0 } );
        }

        #
        #  Write out the output.
        #
        open( OUTPUT, ">", $CONFIG{ 'output' } . "/pubs/" . $sname );
        print OUTPUT $template->output();
        close(OUTPUT);
    }

}


#
#  Create a-z
#
sub createA2Z
{

    #
    #  Process each letter.
    #
    my $letters;
    my $index;


    foreach my $letter ( 'a' ... 'z' )
    {
        my $loop = undef;

        #
        # Look for venues with that name.
        #
        foreach my $tmp ( keys %DATA )
        {
            my $name = $DATA{ $tmp }{ 'name' };
            $name =~ s/^the //gi;
            my $beg = substr( $name, 0, 1 );
            $beg = lc($beg);

            if ( $beg eq $letter )
            {
                push( @$loop, $DATA{ $tmp } );
            }
        }


        if ($loop)
        {
            push( @$index,
                  {  letter  => $letter,
                     entries => $loop
                  } );
        }
        else
        {
            push( @$index, { letter => $letter, } );
        }



        my $count = undef;
        $count = 1 if ($loop);
        push( @$letters,
              {  letter => $letter,
                 count  => $count
              } );

        my @letter = loadTemplate("letter.tmpl");

        my $template =
          HTML::Template->new( arrayref          => \@letter,
                               die_on_bad_params => 0 );

        $template->param( loop => $loop ) if ($loop);
        $template->param( letter => $letter );

        #
        #  Open.
        #
        if ( !-d $CONFIG{ 'output' } . "/pubs/a-z/$letter/" )
        {
            mkpath( $CONFIG{ 'output' } . "/pubs/a-z/$letter/",
                    { verbose => 0 } );
        }

        open( my $handle, ">", "$CONFIG{'output'}/pubs/a-z/$letter/index.html" )
          or
          die "Failed to open: $!";
        print $handle $template->output();
        close($handle);

    }

    #
    #  Now the a-z
    #
    my @az = loadTemplate("a-z.tmpl");
    my $template = HTML::Template->new( arrayref          => \@az,
                                        die_on_bad_params => 0 );

    $template->param( index => $index );
    open( my $handle, ">", "$CONFIG{'output'}/pubs/a-z/index.html" );
    print $handle $template->output();
    close($handle);

}


#
#  Create:
#
#     /data/pubs.xml
#     /data/pubs.json
#
sub createDataPage
{

    #
    #  This nasty code is just to sort the pubs by their name.
    #
    my $data;
    foreach my $key (
        sort {
            lc $DATA{ $a }{ 'name' } cmp lc $DATA{ $b }{ 'name' }
        } keys %DATA
      )
    {

        #
        #  We want to link to *our* site, for each pub.
        #
        my $d = $DATA{ $key };
        $d->{ "link" } =
          "http://edinburgh.io/pubs/" . $d->{ 'sname' } . ".html";

        #
        #  If the pub is closed/replaced then we don't include it
        # in the dump - because the successor should be present
        # instead.
        #
        push( @$data, $d ) unless ( $d->{ 'closed' } );
    }

    #
    #  Ensure we have the output-directory.
    #
    if ( !-d $CONFIG{ 'output' } . "/data" )
    {
        mkpath( $CONFIG{ 'output' } . "/data", { verbose => 0 } );
    }

    #
    # Output the JSON version of the data.
    #
    open( my $handle, ">", "$CONFIG{'output'}/data/pubs.json" );
    print $handle encode_json($data);
    close($handle);

    #
    # Output the JSONP version of the data.
    #
    open( my $jsonp, ">", "$CONFIG{'output'}/data/pubs.jsonp" );
    print $jsonp "pubs(" . encode_json($data) . ")";
    close($jsonp);

    #
    # Output the "XML" version.  Twice.
    #
    my @txt = loadTemplate("index.txt.tmpl");
    my $template = HTML::Template->new( arrayref          => \@txt,
                                        die_on_bad_params => 0 );

    $template->param( xml => $data );
    open( my $xhandle,  ">", "$CONFIG{'output'}/data/pubs.xml" );
    open( my $xthandle, ">", "$CONFIG{'output'}/data/pubs.xml.txt" );
    print $xhandle $template->output();
    print $xthandle $template->output();
    close($xhandle);
    close($xthandle);

    #
    #  Finally output the CSV version.
    #
    my @csv = loadTemplate("csv.tmpl");
    my $csvt = HTML::Template->new( arrayref          => \@csv,
                                    die_on_bad_params => 0 );

    $csvt->param( data => $data );
    open( my $csvhandle, ">", "$CONFIG{'output'}/data/pubs.csv" );
    print $csvhandle $csvt->output();
    close($csvhandle);
}


#
#  Create a page for the most recent N pubs.
#
sub createRecentPage
{
    my @sorted = sort { -M $a <=> -M $b }  (keys %DATA );

    my $loop;
    my $count = 0;
    foreach my $file ( @sorted )
    {
        next if ( $count > 10 );
        push( @$loop, $DATA{$file} );
        $count ++;
    }


    my @recent = loadTemplate("recent.html.tmpl");
    my $csvt = HTML::Template->new( arrayref          => \@recent,
                                    die_on_bad_params => 0 );

    $csvt->param( data => $loop );

    if ( !-d $CONFIG{ 'output' } . "/recent" )
    {
        mkpath( $CONFIG{ 'output' } . "/recent", { verbose => 0 } );
    }

    open( my $handle, ">", "$CONFIG{'output'}/recent/index.html" );
    print $handle $csvt->output();
    close($handle);
}


=begin doc

Load a "file" from beneath the "__DATA__" section.

This resets the DATA handle to allow re-reading more than once, if
necessary, and only works because the __DATA__ section is formatted
as follows:

=for example begin

filename:
  DATA - prefixed by two spaces.
  DATA
filename2:
  DATA - prefixed by two spaces.
  ..

=for example end

=cut

sub loadTemplate
{
    my ($file) = (@_);

    # save the position
    my $data_pos = tell DATA;

    my %hash;
    my $filename = undef;

    while ( my $line = <DATA> )
    {
        chomp($line);
        if ( $line =~ /^  (.*)/ )
        {
            $hash{ $filename } .= $1 . "\n";
        }
        elsif ( $line =~ /^(.*):$/ )
        {
            $filename = $1;
        }
    }

    # reset
    seek DATA, $data_pos, 0;

    my $content = $hash{ $file } || undef;
    die "File not found in __DATA__ section: $file"
      if ( !$content );
    return ($content);
}

__DATA__
venue.tmpl:
  <!DOCTYPE html>
  <html>
  <head>
  <title>Edinburgh Pubs: <!-- tmpl_var name='name' --></title>
  <link href="/css/style.css" rel="stylesheet" media="all">
  <link type="text/plain" rel="author" href="/humans.txt" />
  <meta http-equiv="Content-Type" content="text/html; charset=utf-8"/>
  <link rel="shortcut icon" type="image/x-icon" href="/favicon.ico" />
  <script src="//ajax.googleapis.com/ajax/libs/jquery/1.10.2/jquery.min.js"></script>
  <script src="/js/portfolio.pack.min.js"></script>
  <script type="text/javascript">
    $(document).ready(function() {
            var p = $("#gallery").portfolio({enableKeyboardNavigation: true, loop: true});
            p.init();
    });
  </script>
   <script type="text/javascript">
    var _gaq = _gaq || [];
    _gaq.push(['_setAccount', 'UA-45709040-1']);
    _gaq.push(['_trackPageview']);
    (function() {
      var ga = document.createElement('script'); ga.type = 'text/javascript'; ga.async = true;
      ga.src = ('https:' == document.location.protocol ? 'https://ssl' : 'http://www') + '.google-analytics.com/ga.js';
      var s = document.getElementsByTagName('script')[0]; s.parentNode.insertBefore(ga, s);
    })();
  </script>
  </head>
  <body>
   <div id="page">
      <div id="header">
        <h3><span style="font-size:40px"><a href="/">Edinburgh.io</a></span> - The definitive Edinburgh Pub List.</h3>
      </div>
     <div id="navigation">
     <ul>
      <li><a href="/contact/">Contact</a></li>
      <li><a href="/faq/">FAQ</a></li>
      <li><a href="/pubs/a-z/">A-Z</a></li>
      <li><a href="/pubs/">Home</a></li>
     </ul>
     </div>

    <!-- This is a "microformat" to describe the data on this page programattically. -->
    <div id="location" class="vcard" style="display:none;">
    <div class="adr">
    <a class="url fn" href="<!-- tmpl_var name='link' -->"><!-- tmpl_var name='name' --></a>
    <div class="street-address"><!-- tmpl_var name='address' --></div>
    </div>
    <div class="tel"><!--tmpl_var name='telephone' --></div>
    <abbr class="geo" title="<!-- tmpl_var name='latitude' -->;<!-- tmpl_var name='longitude' -->"><!-- tmpl_var name='name' --></abbr>
    </div>
  <div id="main">
  <p>&nbsp;</p><h1><!-- tmpl_var name='name' --></h1>
  <h2>Summary</h2>
  <blockquote>
  <!-- tmpl_var name='address' -->
  <!-- tmpl_if name='telephone' -->
   <p>Telephone: <!-- tmpl_var name='telephone' --></p>
  <!-- /tmpl_if -->
  <!-- tmpl_if name='website' -->
   <p>Website: <a href="<!-- tmpl_var name='website' -->"><!-- tmpl_var name='website' --></a></p>
  <!-- /tmpl_if -->
  <p>GPS: <a href="/pubs/#lat=<!-- tmpl_var name='latitude' -->,long=<!-- tmpl_var name='longitude' -->"><!-- tmpl_var name='latitude' -->,<!-- tmpl_var name='longitude' --></a></p>
  </blockquote>
  <!-- tmpl_if name='has_images' -->
  <h2>Images</h2>
  <blockquote>
  <div id="gallery">
  <!-- tmpl_loop name='images' -->
    <img data-src="/<!-- tmpl_var name='path' -->" />
  <!-- /tmpl_loop -->
  </div>
  </blockquote>
  <!-- /tmpl_if name='images' -->

  <!-- tmpl_if name='closed' -->
  <h2>Closed</h2>
  <blockquote>
    <p>This pub is now closed, and exists for historical reference only.</p>
  </blockquote>
  <!-- /tmpl_if -->

  <!-- tmpl_if name='previously' -->
  <h2>Previously</h2>
    <blockquote>
    <!-- tmpl_loop name='previously' -->
    <p><a href="/pubs/<!-- tmpl_var name='sname' -->.html"><!-- tmpl_var name='name' --></a>.</p>
    <!-- /tmpl_loop -->
    </blockquote>
   <!-- /tmpl_if name='previously' -->
  </div>
  </body>
  </html>
letter.tmpl:
  <!DOCTYPE html>
  <html>
   <head>
    <title>Pubs beginning with <!-- tmpl_var name='letter' --></title>
    <link href="/css/style.css" rel="stylesheet" media="all">
    <link type="text/plain" rel="author" href="/humans.txt" />
    <link rel="shortcut icon" type="image/x-icon" href="/favicon.ico" />
    <meta http-equiv="Content-Type" content="text/html; charset=utf-8"/>
   <script type="text/javascript">
    var _gaq = _gaq || [];
    _gaq.push(['_setAccount', 'UA-45709040-1']);
    _gaq.push(['_trackPageview']);
    (function() {
      var ga = document.createElement('script'); ga.type = 'text/javascript'; ga.async = true;
      ga.src = ('https:' == document.location.protocol ? 'https://ssl' : 'http://www') + '.google-analytics.com/ga.js';
      var s = document.getElementsByTagName('script')[0]; s.parentNode.insertBefore(ga, s);
    })();
  </script>
  </head>
  <body>
   <div id="page">
      <div id="header">
        <h3><span style="font-size:40px"><a href="/">Edinburgh.io</a></span> - The definitive Edinburgh Pub List.</h3>
      </div>
     <div id="navigation">
     <ul>
      <li><a href="/contact/">Contact</a></li>
      <li><a href="/faq/">FAQ</a></li>
      <li><a href="/pubs/a-z/">A-Z</a></li>
      <li><a href="/pubs/">Home</a></li>
     </ul>
     </div>
  <div id="main">
  <p>&nbsp;</p>
   <h1>Pubs beginning with <!-- tmpl_var name='letter' --></h1>
   <ul>
   <!-- tmpl_loop name='loop' -->
   <li><a href="/pubs/<!-- tmpl_var name='sname' -->.html"><!-- tmpl_var name='name' --></a></li>
   <!-- /tmpl_loop -->
   </ul>
   </div>
   </div>
  </body>
  </html>
a-z.tmpl:
  <!DOCTYPE html>
  <html>
   <head>
    <title>Edinburgh A-Z of Pubs</title>
    <link href="/css/style.css" rel="stylesheet" media="all" />
    <link type="text/plain" rel="author" href="/humans.txt" />
    <link rel="shortcut icon" type="image/x-icon" href="/favicon.ico" />
    <meta http-equiv="Content-Type" content="text/html; charset=utf-8"/>
   <script type="text/javascript">
    var _gaq = _gaq || [];
    _gaq.push(['_setAccount', 'UA-45709040-1']);
    _gaq.push(['_trackPageview']);
    (function() {
      var ga = document.createElement('script'); ga.type = 'text/javascript'; ga.async = true;
      ga.src = ('https:' == document.location.protocol ? 'https://ssl' : 'http://www') + '.google-analytics.com/ga.js';
      var s = document.getElementsByTagName('script')[0]; s.parentNode.insertBefore(ga, s);
    })();
  </script>
  </head>
  <body>
   <div id="page">
      <div id="header">
        <h3><span style="font-size:40px"><a href="/">Edinburgh.io</a></span> - The definitive Edinburgh Pub List.</h3>
      </div>
     <div id="navigation">
     <ul>
      <li><a href="/contact/">Contact</a></li>
      <li><a href="/faq/">FAQ</a></li>
      <li id="selected"><a href="/pubs/a-z/">A-Z</a></li>
      <li><a href="/pubs/">Home</a></li>
     </ul>
     </div>
  <div id="main">
  <p>&nbsp;</p>
   <h1>Edinburgh A-Z of Pubs</h1>
    <blockquote><blockquote>
  <dl>
  <!-- tmpl_loop name='index' -->
     <!-- tmpl_if name='entries' -->
      <dt style="border-bottom: 1px solid grey; width:80%;"><a href="/pubs/a-z/<!-- tmpl_var name='letter' -->/"><!-- tmpl_var name='letter' --></a></dt>
      <dd>
      <ul style="list-style:none;">
      <!-- tmpl_loop name='entries' -->
      <li><a href="/pubs/<!-- tmpl_var name='sname' -->.html"><!-- tmpl_var name='name' --></a></li>
      <!-- /tmpl_loop name='entries' -->
      </ul>
      </dd>
     <!-- tmpl_else -->
      <dt><!-- tmpl_var name='letter' --></dt>
      <dd><p>No pubs added yet, please <a href="/faq/">contribute</a>.</p>
    <!-- /tmpl_if -->
  <!-- /tmpl_loop -->
  </dl>
    </blockquote></blockquote>
  </div></div></body></html>
index.txt.tmpl:
  <!-- tmpl_loop name='xml' -->
  <pub>
  <name><!-- tmpl_var name='name' --></name>
  <sname><!-- tmpl_var name='sname' --></sname>
  <link><!-- tmpl_var name='link' --></link>
  <address><!-- tmpl_var name='address' --></address>
  <latitude><!-- tmpl_var name='latitude' --></latitude>
  <longitude><!-- tmpl_var name='longitude' --></longitude>
  <website><!-- tmpl_var name='website' --></website>
  <telephone><!-- tmpl_var name='telephone' --></telephone>
  </pub>
  <!-- /tmpl_loop -->
csv.tmpl:
  "Name", "Address", "Latitude", "Longitude", "Link"
  <!-- tmpl_loop name='data' -->"<!-- tmpl_var name='name' -->", "<!-- tmpl_var name='address' -->", "<!-- tmpl_var name='latitude' -->", "<!-- tmpl_var name='longitude' -->, "<!-- tmpl_var name='link' -->"
  <!-- /tmpl_loop -->
recent.html.tmpl:
   <!DOCTYPE html>
   <html>
   <head>
   <title>Edinburgh Pubs: Recent Additions</title>
   <link href="/css/style.css" rel="stylesheet" media="all">
   <link type="text/plain" rel="author" href="/humans.txt" />
   <meta http-equiv="Content-Type" content="text/html; charset=utf-8"/>
   <link rel="shortcut icon" type="image/x-icon" href="/favicon.ico" />
   <script type="text/javascript">
    var _gaq = _gaq || [];
    _gaq.push(['_setAccount', 'UA-45709040-1']);
    _gaq.push(['_trackPageview']);
    (function() {
      var ga = document.createElement('script'); ga.type = 'text/javascript'; ga.async = true;
      ga.src = ('https:' == document.location.protocol ? 'https://ssl' : 'http://www') + '.google-analytics.com/ga.js';
      var s = document.getElementsByTagName('script')[0]; s.parentNode.insertBefore(ga, s);
    })();
  </script>
  </head>
  <body>
   <div id="page">
    <div id="header">
      <h3><span style="font-size:40px"><a href="/">Edinburgh.io</a></span> - The definitive Edinburgh Pub List.</h3>
    </div>
   <div id="navigation">
   <ul>
    <li><a href="/contact/">Contact</a></li>
    <li><a href="/faq/">FAQ</a></li>
    <li><a href="/pubs/a-z/">A-Z</a></li>
    <li><a href="/pubs/">Home</a></li>
   </ul>
   </div>
  <!-- This is a "microformat" to describe the data on this page programattically. -->
  <div id="location" class="vcard" style="display:none;">
  <div class="adr">
  <a class="url fn" href="">Alhambra Bar</a>
  <div class="street-address">227 Leith Walk, Edinburgh, EH6 8NX</div>
  </div>
  <div class="tel">+44 131 554 4868</div>
  <abbr class="geo" title="55.96670;-3.17495">Alhambra Bar</abbr>
  </div>
  <div id="main">
  <p>&nbsp;</p><h1>Recent Additions</h1>
  <ul style="list-style:none;">
  <!-- tmpl_loop name='data' -->
   <li><a href="<!-- tmpl_var name='link' -->"><!-- tmpl_var name='name' --></a></li>
  <!-- /tmpl_loop -->
  </ul>
  </div>
  </div>
  </body>
  </html>

