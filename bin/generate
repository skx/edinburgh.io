#!/usr/bin/perl
#
#  To script to read ./data/*.xml and generate a series of static
# html-files.
#
#  The code is a little ropy, but will be improved.
#
# Steve
# --


use strict;
use warnings;

use File::Find;
use File::Path qw/ mkpath /;
use HTML::Template;



#
#  Configuration variables.
#
my %CONFIG;
$CONFIG{ 'input' }   = "./data";
$CONFIG{ 'output' }  = "./htdocs";
$CONFIG{ 'verbose' } = 1;


#
#  The data for all our pubs.
#
my %DATA;

#
#  Find all the XML files for our pubs.
#
#  Build into an (inefficient) data-structure.
#
$CONFIG{ 'verbose' } && print "Finding files beneath: $CONFIG{'input'}\n";
find( { wanted => \&process, no_chdir => 1 }, $CONFIG{ 'input' } );

#
#  Create a single page for each venue.
#
#  TODO: We need some way of marking venues as having been renamed
#
#   e.g.  The current "leith beer co" is the old "waterline".
#
createVenues();


#
#  Create the A-Z & letter-pages
#
createA2Z();


#
#  Create index page.
#
createIndex();


#
#  Create teh data page
#
createDataPage();


#
#  All done
#
exit(0);



#
# Process *.xml.
#
sub process
{
    my ($file) = $File::Find::name;
    return unless ( $file =~ /\.xml$/ );

    $CONFIG{ 'verbose' } && print "Parsing: $file\n";

    open( my $data, "<", $file ) or
      die "Failed to read $file - $!";
    foreach my $line (<$data>)
    {
        chomp($line);
        if ( $line =~ /^\s+<([^>]+)>(.*)<\/[^>]+>$/ )
        {
            $DATA{ $file }{ $1 } = $2;
        }
    }
    close($data);
}


#
#  Create one page for each venue.
#
sub createVenues
{

    #
    #  Now process each venue.
    #
    foreach my $venue ( keys %DATA )
    {

        #
        #  Load the template
        #
        my @venue = loadTemplate("venue.tmpl");

        my $template =
          HTML::Template->new( arrayref          => \@venue,
                               die_on_bad_params => 0 );

        #
        #  The images for this pub
        #
        my $images = undef;
        my $sname  = $DATA{ $venue }{ 'sname' };
        foreach my $img ( sort( glob("images/$sname*") ) )
        {
            push( @$images, { path => $img } );
        }

        $sname .= ".html";

        my $name = $DATA{ $venue }{ 'name' };
        $name =~ s/^the //gi;
        my $beg = substr( $name, 0, 1 );
        $beg = lc($beg);

        $template->param( $DATA{ $venue } );
        $template->param( letter => $beg );

        if ($images)
        {
            $template->param( images => $images, has_images => 1 );
        }
        else
        {
            print "WARNING: $sname missing images\n";
        }

        #
        #  Output.
        #
        if ( !-d $CONFIG{ 'output' } . "/pubs" )
        {
            mkpath( $CONFIG{ 'output' } . "/pubs", { verbose => 0 } );
        }

        open( OUTPUT, ">", $CONFIG{ 'output' } . "/pubs/" . $sname );
        print OUTPUT $template->output();
        close(OUTPUT);
    }

}


#
#  Create a-z
#
sub createA2Z
{

    #
    #  Process each letter.
    #
    my $letters;
    my $index;


    foreach my $letter ( 'a' ... 'z' )
    {
        my $loop = undef;

        #
        # Look for venues with that name.
        #
        foreach my $tmp ( keys %DATA )
        {
            my $name = $DATA{ $tmp }{ 'name' };
            $name =~ s/^the //gi;
            my $beg = substr( $name, 0, 1 );
            $beg = lc($beg);

            if ( $beg eq $letter )
            {
                push( @$loop, $DATA{ $tmp } );
            }
        }


        if ($loop)
        {
            push( @$index,
                  {  letter  => $letter,
                     entries => $loop
                  } );
        }
        else
        {
            push( @$index, { letter => $letter, } );
        }



        my $count = undef;
        $count = 1 if ($loop);
        push( @$letters,
              {  letter => $letter,
                 count  => $count
              } );

        my @letter = loadTemplate("letter.tmpl");

        my $template =
          HTML::Template->new( arrayref          => \@letter,
                               die_on_bad_params => 0 );

        $template->param( loop => $loop ) if ($loop);
        $template->param( letter => $letter );

        #
        #  Open.
        #
        if ( !-d $CONFIG{ 'output' } . "/pubs/a-z/$letter/" )
        {
            mkpath( $CONFIG{ 'output' } . "/pubs/a-z/$letter/",
                    { verbose => 0 } );
        }

        open( my $handle, ">", "$CONFIG{'output'}/pubs/a-z/$letter/index.html" )
          or
          die "Failed to open: $!";
        print $handle $template->output();
        close($handle);

    }

    #
    #  Now the a-z
    #
    my @az = loadTemplate("a-z.tmpl");
    my $template = HTML::Template->new( arrayref          => \@az,
                                        die_on_bad_params => 0 );

    $template->param( index => $index );
    open( my $handle, ">", "$CONFIG{'output'}/pubs/a-z/index.html" );
    print $handle $template->output();
    close($handle);

}


#
#  Create the index-page with all the points.
#
sub createIndex
{
    my $xml;
    foreach my $key (
        sort {
            lc $DATA{ $a }{ 'name' } cmp lc $DATA{ $b }{ 'name' }
        } keys %DATA
      )
    {
        push( @$xml, $DATA{ $key } );
    }

    #
    #  Here.
    #
    my @ind = loadTemplate("index.html.tmpl");
    my $template = HTML::Template->new( arrayref          => \@ind,
                                        die_on_bad_params => 0 );
    $template->param( coords => $xml );
    open( my $ihandle, ">", "$CONFIG{'output'}/pubs/index.html" );
    print $ihandle $template->output();
    close($ihandle);

}



#
#  Create:
#
#     /data/pubs.xml
#     /data/pubs.json
#
sub createDataPage
{
    my $data;
    foreach my $key (
        sort {
            lc $DATA{ $a }{ 'name' } cmp lc $DATA{ $b }{ 'name' }
        } keys %DATA
      )
    {
        push( @$data, $DATA{ $key } );
    }

    #
    #  Convert to JSON and output
    #
    if ( !-d $CONFIG{ 'output' } . "/data" )
    {
        mkpath( $CONFIG{ 'output' } . "/data", { verbose => 0 } );
    }

    #
    # Output JSON
    #
    use JSON;
    my $x = encode_json($data);
    open( my $handle, ">", "$CONFIG{'output'}/data/pubs.json" );
    print $handle $x;
    close($handle);

    #
    # Output text
    #
    my @txt = loadTemplate("index.txt.tmpl");
    my $template = HTML::Template->new( arrayref          => \@txt,
                                        die_on_bad_params => 0 );

    $template->param( xml => $data );
    open( my $xhandle,  ">", "$CONFIG{'output'}/data/pubs.xml" );
    open( my $xthandle, ">", "$CONFIG{'output'}/data/pubs.xml.txt" );
    print $xhandle $template->output();
    print $xthandle $template->output();
    close($xhandle);
    close($xthandle);

}

=begin doc

Load a "file" from beneath the "__DATA__" section.

This resets the DATA handle to allow re-reading more than once, if
necessary, and only works because the __DATA__ section is formatted
as follows:

=for example begin

filename:
  DATA - prefixed by two spaces.
  DATA
filename2:
  DATA - prefixed by two spaces.
  ..

=for example end

=cut

sub loadTemplate
{
    my ($file) = (@_);

    # save the position
    my $data_pos = tell DATA;

    my %hash;
    my $filename = undef;

    while ( my $line = <DATA> )
    {
        chomp($line);
        if ( $line =~ /^  (.*)/ )
        {
            $hash{ $filename } .= $1 . "\n";
        }
        elsif ( $line =~ /^(.*):$/ )
        {
            $filename = $1;
        }
    }

    # reset
    seek DATA, $data_pos, 0;

    my $content = $hash{ $file } || undef;
    die "File not found in __DATA__ seciont: $file"
      if ( !$content );
    return ($content);
}

__DATA__
venue.tmpl:
  <!DOCTYPE html>
  <html>
  <head>
  <title>Edinburgh Pubs: <!-- tmpl_var name='name' --></title>
  <link href="/css/style.css" rel="stylesheet" media="all">
  <link type="text/plain" rel="author" href="/humans.txt" />
  <meta http-equiv="Content-Type" content="text/html; charset=utf-8"/>
  <link rel="shortcut icon" type="image/x-icon" href="/favicon.ico" />
  <script src="//ajax.googleapis.com/ajax/libs/jquery/1.10.2/jquery.min.js"></script>
  <script src="/js/portfolio.pack.min.js"></script>
  <script type="text/javascript">
    $(document).ready(function() {
            var p = $("#gallery").portfolio({enableKeyboardNavigation: true, loop: true});
            p.init();
    });
  </script>
  </head>
  <body>
   <div id="page">
      <div id="header">
        <h3><span style="font-size:40px"><a href="/">Edinburgh.io</a></span> - The definitive Edinburgh Pub List.</h3>
      </div>
     <div id="navigation">
     <ul>
      <li><a href="/contact/">Contact</a></li>
      <li><a href="/faq/">FAQ</a></li>
      <li><a href="/pubs/a-z/">A-Z</a></li>
      <li><a href="/pubs/">Home</a></li>
     </ul>
     </div>
  <div id="main">
  <p>&nbsp;</p><h1><!-- tmpl_var name='name' --></h1>
  <h2>Summary</h2>
  <blockquote>
  <!-- tmpl_var name='address' -->
  <!-- tmpl_if name='telephone' -->
   <p>Telephone: <!-- tmpl_var name='telephone' --></p>
  <!-- /tmpl_if -->
  <!-- tmpl_if name='website' -->
   <p>Website: <a href="<!-- tmpl_var name='website' -->"><!-- tmpl_var name='website' --></a></p>
  <!-- /tmpl_if -->
  <p>GPS: <a href="/pubs/#lat=<!-- tmpl_var name='latitude' -->,long=<!-- tmpl_var name='longitude' -->"><!-- tmpl_var name='latitude' -->,<!-- tmpl_var name='longitude' --></a></p>
  </blockquote>
  <!-- tmpl_if name='has_images' -->
  <h2>Images</h2>
  <blockquote>
  <div id="gallery">
  <!-- tmpl_loop name='images' -->
    <img data-src="/<!-- tmpl_var name='path' -->" />
  <!-- /tmpl_loop -->
  </div>
  </blockquote>
  <!-- /tmpl_if name='images' -->
  <h2>History</h2>
  <blockquote>
  <p>TODO.</p>
  </blockquote>
  </div>
  </body>
  </html>
letter.tmpl:
  <!DOCTYPE html>
  <html>
   <head>
    <title>Pubs beginning with <!-- tmpl_var name='letter' --></title>
    <link href="/css/style.css" rel="stylesheet" media="all">
    <link type="text/plain" rel="author" href="/humans.txt" />
    <link rel="shortcut icon" type="image/x-icon" href="/favicon.ico" />
    <meta http-equiv="Content-Type" content="text/html; charset=utf-8"/>
  </head>
  <body>
   <div id="page">
      <div id="header">
        <h3><span style="font-size:40px"><a href="/">Edinburgh.io</a></span> - The definitive Edinburgh Pub List.</h3>
      </div>
     <div id="navigation">
     <ul>
      <li><a href="/contact/">Contact</a></li>
      <li><a href="/faq/">FAQ</a></li>
      <li><a href="/pubs/a-z/">A-Z</a></li>
      <li><a href="/pubs/">Home</a></li>
     </ul>
     </div>
  <div id="main">
  <p>&nbsp;</p>
   <h1>Pubs beginning with <!-- tmpl_var name='letter' --></h1>
   <ul>
   <!-- tmpl_loop name='loop' -->
   <li><a href="/pubs/<!-- tmpl_var name='sname' -->.html"><!-- tmpl_var name='name' --></a></li>
   <!-- /tmpl_loop -->
   </ul>
   </div>
   </div>
  </body>
  </html>
a-z.tmpl:
  <!DOCTYPE html>
  <html>
   <head>
    <title>Edinburgh A-Z of Pubs</title>
    <link href="/css/style.css" rel="stylesheet" media="all" />
    <link type="text/plain" rel="author" href="/humans.txt" />
    <link rel="shortcut icon" type="image/x-icon" href="/favicon.ico" />
    <meta http-equiv="Content-Type" content="text/html; charset=utf-8"/>
  </head>
  <body>
   <div id="page">
      <div id="header">
        <h3><span style="font-size:40px"><a href="/">Edinburgh.io</a></span> - The definitive Edinburgh Pub List.</h3>
      </div>
     <div id="navigation">
     <ul>
      <li><a href="/contact/">Contact</a></li>
      <li><a href="/faq/">FAQ</a></li>
      <li id="selected"><a href="/pubs/a-z/">A-Z</a></li>
      <li><a href="/pubs/">Home</a></li>
     </ul>
     </div>
  <div id="main">
  <p>&nbsp;</p>
   <h1>Edinburgh A-Z of Pubs</h1>
    <blockquote><blockquote>
  <dl>
  <!-- tmpl_loop name='index' -->
     <!-- tmpl_if name='entries' -->
      <dt style="border-bottom: 1px solid grey; width:80%;"><a href="/pubs/a-z/<!-- tmpl_var name='letter' -->/"><!-- tmpl_var name='letter' --></a></dt>
      <dd>
      <ul style="list-style:none;">
      <!-- tmpl_loop name='entries' -->
      <li><a href="/pubs/<!-- tmpl_var name='sname' -->.html"><!-- tmpl_var name='name' --></a></li>
      <!-- /tmpl_loop name='entries' -->
      </ul>
      </dd>
     <!-- tmpl_else -->
      <dt><!-- tmpl_var name='letter' --></dt>
      <dd><p>No pubs added yet, please <a href="/faq/">contribute</a>.</p>
    <!-- /tmpl_if -->
  <!-- /tmpl_loop -->
  </dl>
    </blockquote></blockquote>
  </div></div></body></html>
index.txt.tmpl:
  <!-- tmpl_loop name='xml' -->
  <pub>
  <name><!-- tmpl_var name='name' --></name>
  <address><!-- tmpl_var name='address' --></address>
  <latitude><!-- tmpl_var name='latitude' --></latitude>
  <longitude><!-- tmpl_var name='longitude' --></longitude>
  <website><!-- tmpl_var name='website' --></website>
  <telephone><!-- tmpl_var name='telephone' --></telephone>
  </pub>
  <!-- /tmpl_loop -->
index.html.tmpl:
  <!DOCTYPE html>
  <html>
  <head>
  <title>Edinburgh Pubs</title>
  <link href="/css/style.css" rel="stylesheet" media="all" />
  <link type="text/plain" rel="author" href="/humans.txt" />
  <link rel="shortcut icon" type="image/x-icon" href="/favicon.ico" />
  <meta http-equiv="Content-Type" content="text/html; charset=utf-8"/>
  <script src="/js/jquery.min.js"></script>
  <script src="/js/portfolio.pack.min.js"></script>
  <script src="http://maps.googleapis.com/maps/api/js?key=AIzaSyDY0kkJiTPVd2U7aTOAwhc9ySH6oHxOIYM&sensor=false"></script>
  <script>
  var infowindow;
  (function () {
    google.maps.Map.prototype.markers = new Array();
     google.maps.Map.prototype.addMarker = function(marker) {
     this.markers[this.markers.length] = marker;
    };
    google.maps.Map.prototype.getMarkers = function() {
     return this.markers
    };
    google.maps.Map.prototype.clearMarkers = function() {
    if(infowindow) {
      infowindow.close();
    }
    for(var i=0; i<this.markers.length; i++){
      this.markers[i].set_map(null);
    }
  };
  })();
  function createMarker(map,latlng,name,link) {
    var marker = new google.maps.Marker({position: latlng, map: map});
    google.maps.event.addListener(marker, "click", function() {
      if (infowindow) infowindow.close();
      infowindow = new google.maps.InfoWindow({content: "<div><p><a href=\"" + link + "\">" + name + "</a></p></div>" });
      infowindow.open(map, marker);
    });
    return marker;
  }
  var myCenter=new google.maps.LatLng(55.97561,-3.17024);
  var zoom = 13;
  function initialize()
  {
    if(window.location.hash) {
      var r = /^#lat=([0-9.]+),long=(.*)$/;
      var m = r.exec( window.location.hash );
      if ( m )
      {
         myCenter=new google.maps.LatLng( m[1], m[2] );
         zoom = 17;
      }
  }
  var mapProp = { center:myCenter, zoom:zoom, mapTypeId:google.maps.MapTypeId.ROADMAP  };
  var map=new google.maps.Map(document.getElementById("googleMap"),mapProp);
  var coords = [
  <!-- tmpl_loop name='coords' -->
  [<!-- tmpl_var name='latitude' -->,<!-- tmpl_var name='longitude' -->,"<!-- tmpl_var name='name' -->", "/pubs/<!-- tmpl_var name='sname' -->.html"],
  <!-- /tmpl_loop -->
  ];

  // iterate over your coords array
  for (var i = 0; i < coords.length; i++) {
        // create a closure for your latitude/longitude pair
        (function(coord) {
            // set the location...
            var latLng = new google.maps.LatLng(coord[0], coord[1]);
            map.addMarker(createMarker(map,latLng,coord[2],coord[3]));
        })(coords[i]);
    };
  }
  google.maps.event.addDomListener(window, 'load', initialize);
  </script>
  </head>
  <body>
  <div id="fb-root"></div>
  <script>(function(d, s, id) {
  var js, fjs = d.getElementsByTagName(s)[0];
  if (d.getElementById(id)) return;
  js = d.createElement(s); js.id = id;
  js.src = "//connect.facebook.net/en_GB/all.js#xfbml=1";
  fjs.parentNode.insertBefore(js, fjs);
  }(document, 'script', 'facebook-jssdk'));</script>

   <div id="page">
      <div id="header">
        <h3><span style="font-size:40px"><a href="/">Edinburgh.io</a></span> - The definitive Edinburgh Pub List.</h3>
      </div>
     <div id="navigation">
     <ul>
      <li><a href="/contact/">Contact</a></li>
      <li><a href="/faq/">FAQ</a></li>
      <li><a href="/pubs/a-z/">A-Z</a></li>
      <li id="selected"><a href="/pubs/">Home</a></li>
     </ul>
     </div>
  <div id="main">
  <p>&nbsp;</p>
  <table>
  <tr><td id="fb" style="width:180px"><p><small><a href="#" onClick="$('#fb').hide(); return false;">hide</a></small></p>
  <div class="fb-like-box" data-href="http://www.facebook.com/edinburgh.io" data-height="250" data-colorscheme="light" data-show-faces="true" data-header="false" data-stream="false" data-show-border="false"></div></td>
      <td>
       <div style="text-align:center;width:100%;">
       <div id="googleMap" style="display:inline-block;width:90%;height:450px;"></div>
       </div>
      </td></tr></table>
  <p style="text-align:right;"><small>This site is generated from <a href="/data/">simple meta-data</a>, which is open to collaboration <a href="https://github.com/skx/edinburgh.io/">on github</a>.</small></p>
  </div>
  </div>
  </body>
  </html>
